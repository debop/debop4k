/*
 * Copyright (c) 2016. sunghyouk.bae@gmail.com
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    ext.kotlinVersion = '1.0.2-1'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'

    group = 'com.github.debop'
    version = '0.0.1-SNAPSHOT'

    ext.springVersion = '4.2.6.RELEASE'
    ext.funktionaleVersion = '0.8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    compileJava {
        sourceCompatibility = '1.6'
        targetCompatibility = '1.6'
    }

    compileTestJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    sourceSets {
        main.java.srcDirs = ['src/java']
        main.kotlin.srcDirs = ['src/kotlin']
        main.resources.srcDirs = ['src/resources']

        test.java.srcDirs = ['test/java']
        test.kotlin.srcDirs = ['test/kotlin']
        test.resources.srcDirs = ['test/resources']
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/exposed" }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

        // functional programming for kotlin
        compile "org.funktionale:funktionale:$funktionaleVersion"

        compile 'org.slf4j:slf4j-api:1.7.+'
        runtime 'ch.qos.logback:logback-classic:1.1.+'
        compile 'javax.inject:javax.inject:1'

        optional 'nl.komponents.kovenant:kovenant:3.2.+'
        optional "uy.klutter:klutter-core:1.18.+"
        optional "uy.klutter:klutter-json-jackson:1.18.+"
        optional "uy.klutter:klutter-reflect:1.18.+"
        optional "uy.kohesive.injekt:injekt-core:1.16.+"

        compile "org.jetbrains.exposed:exposed:0.5.0"

        compile "joda-time:joda-time:2.9.+"

        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.4.1'
        testCompile 'org.assertj:assertj-joda-time:2.0.0'
        testCompile 'io.kotlintest:kotlintest:1.2.0'
    }
}

defaultTasks 'clean', 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

test {
    jvmArgs "-XX:MaxPermSize=512m"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}







